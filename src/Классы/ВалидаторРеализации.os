#Использовать annotations
#Использовать reflector

// КонтейнерАннотаций
//
Перем _КонтейнерАннотаций;

// ОпределениеАннотации - определение аннотации &Реализует.
//
Перем _ОпределениеАннотации;

// Проверить, что объект реализует все интерфейсы, указанные в аннотации &Реализует.
// Если объект не реализует какой-либо из интерфейсов, будет вызвано исключение.
//
// Параметры:
// ПроверяемыйОбъект - Сценарий - объект, реализующий интерфейсы.
//
Процедура ИнтерфейсыРеализованыКорректно(ПроверяемыйОбъект) Экспорт
	
	РефлекторОбъекта = Новый РефлекторОбъекта(ПроверяемыйОбъект);
	МетодыСАннотацией = РефлекторОбъекта.ПолучитьТаблицуМетодов("Реализует", Ложь);
	
	Если МетодыСАннотацией.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для Каждого Метод Из МетодыСАннотацией Цикл
		
		Аннотации = РаботаСАннотациями.НайтиАннотации(Метод.Аннотации, "Реализует");
		
		Для Каждого Аннотация Из Аннотации Цикл
			ОбъектАннотации = _ОпределениеАннотации.СоздатьОбъектАннотации(Аннотация);
			ИмяИнтерфейса = ОбъектАннотации.ИмяИнтерфейса();
			
			ИнтерфейсОбъекта = Новый ИнтерфейсОбъекта();
			ИнтерфейсОбъекта.ИзОбъекта(Вычислить(ИмяИнтерфейса));
			
			ВызватьИсключениеРеализации = Истина;
			РефлекторОбъекта.РеализуетИнтерфейс(ИнтерфейсОбъекта, ВызватьИсключениеРеализации);
		КонецЦикла;
	
	КонецЦикла;
КонецПроцедуры

// Класс для проверки корректности реализации интерфейсов объектом
//
// Параметры:
//  КонтейнерАннотаций - КонтейнерАннотаций - задаваемый из-вне контейнер аннотаций.
//                                            Позволяет использовать общий контейнер аннотаций для всего приложения.
Процедура ПриСозданииОбъекта(КонтейнерАннотаций = Неопределено)
	Если КонтейнерАннотаций = Неопределено Тогда
		_КонтейнерАннотаций = Новый КонтейнерАннотаций();
		_КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияРеализует"));
	Иначе
		_КонтейнерАннотаций = КонтейнерАннотаций;
	КонецЕсли;

	_ОпределениеАннотации = _КонтейнерАннотаций.ПолучитьОпределениеАннотации("Реализует");

КонецПроцедуры