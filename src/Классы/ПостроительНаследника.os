#Использовать annotations
#Использовать decorator
#Использовать reflector

Перем _КонтейнерАннотаций;

Перем _ОбъектНаследник;
Перем _ОбъектРодитель;
Перем _МетодыНаследника;
Перем _КонструкторНаследника;

Функция Построить() Экспорт

	Если _КонтейнерАннотаций = Неопределено Тогда
		_КонтейнерАннотаций = Новый КонтейнерАннотаций();
		_КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияРасширяет"));
	КонецЕсли;

	РефлекторОбъекта = Новый РефлекторОбъекта(_ОбъектНаследник);
	
	// TODO: Мета-аннотация &Родитель
	СвойстваНаследника_Родитель = РефлекторОбъекта.ПолучитьТаблицуСвойств("Родитель", Истина);
	Если _МетодыНаследника = Неопределено Тогда
		МетодыНаследника = РефлекторОбъекта.ПолучитьТаблицуМетодов(Неопределено, Ложь);
	Иначе
		МетодыНаследника = _МетодыНаследника;
	КонецЕсли;
	Если _КонструкторНаследника = Неопределено Тогда
		КонструкторНаследника = РаботаСАннотациями.НайтиМетодыСАннотацией(МетодыНаследника, "Расширяет")[0];
	Иначе
		КонструкторНаследника = _КонструкторНаследника;
	КонецЕсли;
	
	ОпределениеАннотацииРасширяет = _КонтейнерАннотаций.ПолучитьОпределениеАннотации("Расширяет");
	АннотацияРасширяет = РаботаСАннотациями.НайтиАннотацию(КонструкторНаследника.Аннотации, "Расширяет");
	
	ОбъектАннотацииРасширяет = ОпределениеАннотацииРасширяет.СоздатьОбъектАннотации(АннотацияРасширяет);

	ИмяПоляОбъектРодитель = "_ОбъектРодитель";
	ИмяТипаРодителя = ОбъектАннотацииРасширяет.ИмяТипаРодителя();
	РефлекторОбъектаРодителя = Новый РефлекторОбъекта(Тип(ИмяТипаРодителя));
	МетодыРодителя = РефлекторОбъектаРодителя.ПолучитьТаблицуМетодов(Неопределено, Ложь);
	
	Декоратор = Новый ПостроительДекоратора(_ОбъектНаследник);

	Если СвойстваНаследника_Родитель.Количество() = 0 Тогда
		Поле = Новый Поле("_ОбъектРодитель")
			.ЗначениеПоУмолчанию(_ОбъектРодитель);
	
		Декоратор.Поле(Поле);
	Иначе
		Рефлектор = Новый Рефлектор();
		Для Каждого СвойствоНаследника_Родитель Из СвойстваНаследника_Родитель Цикл
			Рефлектор.УстановитьСвойство(_ОбъектНаследник, СвойствоНаследника_Родитель.Имя, _ОбъектРодитель);
			ИмяПоляОбъектРодитель = СвойствоНаследника_Родитель.Имя;
		КонецЦикла;
	КонецЕсли;

	Для Каждого МетодРодителя Из МетодыРодителя Цикл
		
		Если НЕ МетодРодителя.Экспорт Тогда
			Продолжить;
		КонецЕсли;

		Если МетодРодителя.ЭтоФункция И РефлекторОбъекта.ЕстьФункция(МетодРодителя.Имя, МетодРодителя.КоличествоПараметров) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ МетодРодителя.ЭтоФункция И РефлекторОбъекта.ЕстьПроцедура(МетодРодителя.Имя, МетодРодителя.КоличествоПараметров) Тогда
			Продолжить;
		КонецЕсли;

		Метод = Новый Метод(МетодРодителя.Имя).Публичный();

		ПеренестиАннотации(Метод, МетодРодителя.Аннотации);
		
		ИменаПараметров = Новый Массив;
		Для Каждого ПараметрРодителя Из МетодРодителя.Параметры Цикл
			ПараметрМетода = ПараметрМетодаИзДанныхРефлектора(ПараметрРодителя);
			Метод.Параметр(ПараметрМетода);

			ИменаПараметров.Добавить(ПараметрРодителя.Имя);
		КонецЦикла;
		СтрокаПараметры = СтрСоединить(ИменаПараметров, ", ");
		
		ТелоМетода = ИмяПоляОбъектРодитель + "." + МетодРодителя.Имя + "(" + СтрокаПараметры + ");";
		Если МетодРодителя.ЭтоФункция Тогда
			ТелоМетода = "Возврат " + ТелоМетода;
		Иначе
			Метод.ЭтоПроцедура();
		КонецЕсли;
		Метод.ТелоМетода(ТелоМетода);

		Декоратор.Метод(Метод);

	КонецЦикла;

	// Перенос конструктора
	КонструкторРодителя = МетодыРодителя.Найти("ПриСозданииОбъекта", "Имя");

	Если КонструкторНаследника <> Неопределено ИЛИ КонструкторРодителя <> Неопределено Тогда
		Метод = Новый Метод(КонструкторНаследника.Имя).ЭтоПроцедура();
		
		Если КонструкторНаследника <> Неопределено Тогда
			ПеренестиАннотации(Метод, КонструкторНаследника.Аннотации, "Расширяет");
		КонецЕсли;

		Если КонструкторРодителя <> Неопределено Тогда
			ПеренестиАннотации(Метод, КонструкторРодителя.Аннотации, "Расширяет");
		КонецЕсли;

		Декоратор.Метод(Метод);
	КонецЕсли;
	
	ПредставлениеТипа = "" + ТипЗнч(_ОбъектНаследник) + "_Расширяет_" + ИмяТипаРодителя;
	Метод = Новый Метод("ОбработкаПолученияПредставления")
		.ЭтоПроцедура()
		.Параметр(Новый ПараметрМетода("Представление"))
		.Параметр(Новый ПараметрМетода("СтандартнаяОбработка"))
		.ТелоМетода("СтандартнаяОбработка = Ложь; Представление = """ + ПредставлениеТипа + """;");
	Декоратор.Метод(Метод);

	Объект = Декоратор.Построить();

	Возврат Объект;
КонецФункции

Процедура ПеренестиАннотации(МетодИлиПараметр, Аннотации, ИмяИсключаемойАннотации = "")
	Для Каждого ДанныеАннотации Из Аннотации Цикл
		Если ВРег(ДанныеАннотации.Имя) = ВРег(ИмяИсключаемойАннотации) Тогда
			Продолжить;
		КонецЕсли;

		Аннотация = АннотацияИзДанныхРефлектора(ДанныеАннотации);
		МетодИлиПараметр.Аннотация(Аннотация);
	КонецЦикла;
КонецПроцедуры

Функция ПараметрМетодаИзДанныхРефлектора(Параметр)

	ПараметрМетода = Новый ПараметрМетода(Параметр.Имя);
	Если Параметр.ПоЗначению Тогда
		ПараметрМетода.ПоЗначению();
	КонецЕсли;
	Если Параметр.ЕстьЗначениеПоУмолчанию Тогда
		ПараметрМетода.ЗначениеПоУмолчанию(Параметр.ЗначениеПоУмолчанию);
	КонецЕсли;

	ПеренестиАннотации(ПараметрМетода, Параметр.Аннотации);

	Возврат ПараметрМетода;

КонецФункции

Процедура УстановитьКонтейнерАннотаций(КонтейнерАннотаций) Экспорт
	_КонтейнерАннотаций = КонтейнерАннотаций;
КонецПроцедуры

Функция АннотацияИзДанныхРефлектора(ДанныеАннотации)
	
	Аннотация = Новый Аннотация(ДанныеАннотации.Имя);
	Для Каждого Параметр Из ДанныеАннотации.Параметры Цикл
		Аннотация.Параметр(Параметр.Значение, Параметр.Имя);
	КонецЦикла;

	Возврат Аннотация;

КонецФункции

Процедура ПриСозданииОбъекта(ОбъектНаследник, ОбъектРодитель, МетодыНаследника = Неопределено, КонструкторНаследника = Неопределено)
	_ОбъектНаследник = ОбъектНаследник;
	_ОбъектРодитель = ОбъектРодитель;
	_МетодыНаследника = МетодыНаследника;
	_КонструкторНаследника = КонструкторНаследника;
КонецПроцедуры
