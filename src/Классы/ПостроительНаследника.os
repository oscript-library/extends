#Использовать annotations
#Использовать decorator
#Использовать reflector

// КонтейнерАннотаций
//
Перем _КонтейнерАннотаций;

// Сценарий
//
Перем _ОбъектНаследник;

// Сценарий
//
Перем _ОбъектРодитель;

// ТаблицаЗначений
//
Перем _МетодыНаследника;

// СтрокаТаблицыЗначений
//
Перем _КонструкторНаследника;

// РефлекторОбъекта
//
Перем _РефлекторОбъекта;

// Выполняет связывание объекта-наследника с объектом-родителем и возвращает ПостроительДекоратора,
// который содержит методы и поля объекта-наследника и методы объекта-родителя.
//
// Поле объекта-наследника, помеченное аннотаций &Родитель, будет заполнено объектом-родителем.
// Если аннотация не найдена, будет для хранения родителя будет создано поле с именем "_ОбъектРодитель".
//
// Объект-наследник может содержать методы, совпадающие по именам с методами объекта-родителя. При необходимости
// метод объекта-наследника может вызывать метод объекта-родителя через поле-родитель.
// 
// Объект-наследник должен иметь аннотацию &Расширяет, в которой указано имя типа объекта-родителя.
//
// Возвращаемое значение:
//  ПостроительДекоратора - полностью сконфигурированный построитель декоратора, из которого можно получить
//                          итоговый класс-наследник.
//
Функция СоздатьДекоратор() Экспорт

	ИмяАннотацииРасширяет = "Расширяет";

	Если _КонтейнерАннотаций = Неопределено Тогда
		_КонтейнерАннотаций = Новый КонтейнерАннотаций();
		_КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияРасширяет"));
	КонецЕсли;
	
	Если _МетодыНаследника = Неопределено Тогда
		МетодыНаследника = _РефлекторОбъекта.ПолучитьТаблицуМетодов(Неопределено, Ложь);
	Иначе
		МетодыНаследника = _МетодыНаследника;
	КонецЕсли;
	Если _КонструкторНаследника = Неопределено Тогда
		КонструкторНаследника = РаботаСАннотациями.НайтиМетодыСАннотацией(МетодыНаследника, ИмяАннотацииРасширяет)[0];
	Иначе
		КонструкторНаследника = _КонструкторНаследника;
	КонецЕсли;
	
	ОпределениеАннотацииРасширяет = _КонтейнерАннотаций.ПолучитьОпределениеАннотации(ИмяАннотацииРасширяет);
	АннотацияРасширяет = РаботаСАннотациями.НайтиАннотацию(КонструкторНаследника.Аннотации, ИмяАннотацииРасширяет);
	
	ОбъектАннотацииРасширяет = ОпределениеАннотацииРасширяет.СоздатьОбъектАннотации(АннотацияРасширяет);

	ИмяТипаРодителя = ОбъектАннотацииРасширяет.ИмяТипаРодителя();
	РефлекторОбъектаРодителя = Новый РефлекторОбъекта(Тип(ИмяТипаРодителя));
	МетодыРодителя = РефлекторОбъектаРодителя.ПолучитьТаблицуМетодов(Неопределено, Ложь);
	
	Декоратор = Новый ПостроительДекоратора(_ОбъектНаследник);

	ИмяПоляОбъектРодитель = ИмяПоляОбъектРодитель();
	Если _РефлекторОбъекта.ЕстьСвойство(ИмяПоляОбъектРодитель) Тогда
		Рефлектор = Новый Рефлектор();
		Рефлектор.УстановитьСвойство(_ОбъектНаследник, ИмяПоляОбъектРодитель, _ОбъектРодитель);
	Иначе
		Поле = Новый Поле(ИмяПоляОбъектРодитель)
			.ЗначениеПоУмолчанию(_ОбъектРодитель);
	
		Декоратор.Поле(Поле);
	КонецЕсли;

	Для Каждого МетодРодителя Из МетодыРодителя Цикл
		
		Если НЕ МетодРодителя.Экспорт Тогда
			Продолжить;
		КонецЕсли;

		Если МетодРодителя.ЭтоФункция 
			И _РефлекторОбъекта.ЕстьФункция(МетодРодителя.Имя, МетодРодителя.КоличествоПараметров) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ МетодРодителя.ЭтоФункция 
			И _РефлекторОбъекта.ЕстьПроцедура(МетодРодителя.Имя, МетодРодителя.КоличествоПараметров) Тогда
			Продолжить;
		КонецЕсли;

		Метод = Новый Метод(МетодРодителя.Имя).Публичный();

		ПеренестиАннотации(Метод, МетодРодителя.Аннотации);
		
		ИменаПараметров = Новый Массив;
		Для Каждого ПараметрРодителя Из МетодРодителя.Параметры Цикл
			ПараметрМетода = ПараметрМетодаИзДанныхРефлектора(ПараметрРодителя);
			Метод.Параметр(ПараметрМетода);

			ИменаПараметров.Добавить(ПараметрРодителя.Имя);
		КонецЦикла;
		СтрокаПараметры = СтрСоединить(ИменаПараметров, ", ");
		
		ТелоМетода = ИмяПоляОбъектРодитель + "." + МетодРодителя.Имя + "(" + СтрокаПараметры + ");";
		Если МетодРодителя.ЭтоФункция Тогда
			ТелоМетода = "Возврат " + ТелоМетода;
		Иначе
			Метод.ЭтоПроцедура();
		КонецЕсли;
		Метод.ТелоМетода(ТелоМетода);

		Декоратор.Метод(Метод);

	КонецЦикла;

	// Перенос конструктора
	КонструкторРодителя = МетодыРодителя.Найти("ПриСозданииОбъекта", "Имя");

	Если КонструкторРодителя <> Неопределено Тогда
		Метод = Новый Метод(КонструкторНаследника.Имя).ЭтоПроцедура();
		
		ПеренестиАннотации(Метод, КонструкторНаследника.Аннотации, ИмяАннотацииРасширяет);
		
		Если КонструкторРодителя <> Неопределено Тогда
			ПеренестиАннотации(Метод, КонструкторРодителя.Аннотации, ИмяАннотацииРасширяет);
		КонецЕсли;

		Декоратор.Метод(Метод);
	КонецЕсли;
	
	ПредставлениеТипа = "" + ТипЗнч(_ОбъектНаследник) + "_Расширяет_" + ИмяТипаРодителя;
	Метод = Новый Метод("ОбработкаПолученияПредставления")
		.ЭтоПроцедура()
		.Параметр(Новый ПараметрМетода("Представление"))
		.Параметр(Новый ПараметрМетода("СтандартнаяОбработка"))
		.ТелоМетода("СтандартнаяОбработка = Ложь; Представление = """ + ПредставлениеТипа + """;");
	Декоратор.Метод(Метод);

	Возврат Декоратор;

КонецФункции

// Получить имя поля, в котором хранится объект-родитель.
// Возвращается имя поля, помеченное аннотацией &Родитель, или "_ОбъектРодитель", если аннотация не найдена.
//
// Возвращаемое значение:
//   Строка
//
Функция ИмяПоляОбъектРодитель() Экспорт

	СвойстваНаследника_Родитель = _РефлекторОбъекта.ПолучитьТаблицуСвойств("Родитель", Истина);
	Если СвойстваНаследника_Родитель.Количество() = 0 Тогда
		Возврат "_ОбъектРодитель";
	Иначе
		Возврат СвойстваНаследника_Родитель[0].Имя;
	КонецЕсли;

КонецФункции

// Выполнить связывание объекта-наследника с объектом-родителем и вернуть результирующий класс
// с общим интерфейсом.
// См. СоздатьДекоратор()
//
// Возвращаемое значение:
//  Сценарий - класс-наследник с общим интерфейсом
//
Функция Построить() Экспорт

	Возврат СоздатьДекоратор().Построить();

КонецФункции

// Сеттер для контейнера аннотаций.
// Позволяет использовать общий контейнер аннотаций для всего приложения.
//
// Параметры:
//  КонтейнерАннотаций - КонтейнерАннотаций - устанавливаемый контейнер аннотаций.
//
Процедура УстановитьКонтейнерАннотаций(КонтейнерАннотаций) Экспорт
	_КонтейнерАннотаций = КонтейнерАннотаций;
КонецПроцедуры

Процедура ПеренестиАннотации(МетодИлиПараметр, Аннотации, ИмяИсключаемойАннотации = "")
	Для Каждого ДанныеАннотации Из Аннотации Цикл
		Если ВРег(ДанныеАннотации.Имя) = ВРег(ИмяИсключаемойАннотации) Тогда
			Продолжить;
		КонецЕсли;

		Аннотация = АннотацияИзДанныхРефлектора(ДанныеАннотации);
		МетодИлиПараметр.Аннотация(Аннотация);
	КонецЦикла;
КонецПроцедуры

Функция ПараметрМетодаИзДанныхРефлектора(Параметр)

	ПараметрМетода = Новый ПараметрМетода(Параметр.Имя);
	Если Параметр.ПоЗначению Тогда
		ПараметрМетода.ПоЗначению();
	КонецЕсли;
	Если Параметр.ЕстьЗначениеПоУмолчанию Тогда
		ПараметрМетода.ЗначениеПоУмолчанию(Параметр.ЗначениеПоУмолчанию);
	КонецЕсли;

	ПеренестиАннотации(ПараметрМетода, Параметр.Аннотации);

	Возврат ПараметрМетода;

КонецФункции

Функция АннотацияИзДанныхРефлектора(ДанныеАннотации)
	
	Аннотация = Новый Аннотация(ДанныеАннотации.Имя);
	Для Каждого Параметр Из ДанныеАннотации.Параметры Цикл
		Аннотация.Параметр(Параметр.Значение, Параметр.Имя);
	КонецЦикла;

	Возврат Аннотация;

КонецФункции

// Класс, выполняющий связывание объекта-наследника с объектом-родителем и создающий класс-обертку (декоратор)
// с общим интерфейсом.
//
// Параметры:
//  ОбъектНаследник - Сценарий - объект-наследник. Должен содержать аннотацию &Расширяет.
//  ОбъектРодитель - Сценарий - объект-родитель.
//  МетодыНаследника - ТаблицаЗначений - методы объекта-наследника, как они возвращаются рефлектором.
//                                       Может использоваться для сокращения использования рефлектора или
//                                       необходимости дополнительной обработки методов, например, разворачивания
//                                       аннотаций библиотекой annotations.
//  КонструкторНаследника - СтрокаТаблицыЗначений - данные о конструкторе объекта-наследника,
//                                                  как они возвращаются рефлектором.
Процедура ПриСозданииОбъекта(
	ОбъектНаследник,
	ОбъектРодитель,
	МетодыНаследника = Неопределено,
	КонструкторНаследника = Неопределено
)
	_ОбъектНаследник = ОбъектНаследник;
	_ОбъектРодитель = ОбъектРодитель;
	_МетодыНаследника = МетодыНаследника;
	_КонструкторНаследника = КонструкторНаследника;

	_РефлекторОбъекта = Новый РефлекторОбъекта(_ОбъектНаследник);
КонецПроцедуры
